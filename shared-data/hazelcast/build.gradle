plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

dependencies {
    compileOnly project(":shared-data")
    compileOnly project(":shared-data:simple")
    compileOnly "com.hazelcast:hazelcast:$hazelcast_version"
    compileOnly "io.vertx:vertx-core:$vertx_version"
    compileOnly "com.google.inject:guice:$guice_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation project(":shared-data")
    testImplementation project(":shared-data:simple")
    testImplementation "com.hazelcast:hazelcast:$hazelcast_version"
    testImplementation "io.vertx:vertx-web:$vertx_version"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin
    from sourceSets.main.java
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootProject.group
            def projectHierarchy = new ArrayList<String>()
            def currentProject = project
            while (currentProject != null) {
                projectHierarchy.add(0, currentProject.name)
                currentProject = currentProject.parent
            }
            artifactId = projectHierarchy.join("-")
            version = rootProject.project.version
            from components.java
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            url = s3ArtifactsUrl
            credentials(AwsCredentials) {
                accessKey awsAccessKey
                secretKey awsSecretKey
            }
        }
    }
}
